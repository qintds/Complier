Program -> ExtDefList
ExtDefList -> ExtDefList ExtDef | ExtDef
ExtDef -> ImportDeclaration
        | ClassDeclaration
        | FuncDeclaration
        | StmtList

ImportDeclaration -> IMPORT PackageChain
PackageChain -> IDENTIFIER | PackageChain . IDENTIFIER

ClassDeclaration -> CLASS IDENTIFIER : Super ClassBody
ClassDeclaration -> CLASS IDENTIFIER ClassBody
Super -> Super , PackageChain | PackageChain
ClassBody -> { ClassBodyDeclarations }
ClassBodyDeclarations -> ClassBodyDeclaration
                       | ClassBodyDeclarations ClassBodyDeclaration
ClassBodyDeclaration -> ClassMemberDeclaration
                       | FuncDeclaration
ClassMemberDeclaration -> Assignment

FuncDeclaration -> FUNC IDENTIFIER ( ParamList ) CompSt
FuncDeclaration -> FUNC IDENTIFIER ( ) CompSt

ParamList -> IDENTIFIER DefaultValue | ParamList , IDENTIFIER DefaultValue
ParamList -> IDENTIFIER | ParamList , IDENTIFIER
DefaultValue -> = Primary

Dictionary -> { MapList }
Dictionary -> { }
MapList -> MapList , Map | Map
Map -> Primary : Item
List -> [ ItemList ]
List -> [  ]
ItemList -> ItemList , Item | Item
TupleStart ->  Item , Item  TupleFollow
TupleStart ->  Item , Item
TupleFollow -> TupleFollow , Item | Item
Item -> Primary | Dictionary | List | Tuple

Tuple -> ( TupleStart )
TupleNoBracket -> TupleStart

ListAndTuple -> List | Tuple | TupleNoBracket



CompSt -> { StmtList }
CompSt -> {  }
StmtList -> StmtList Stmt | Stmt
Stmt -> Exp ENTER
      | MultiAssignment ENTER
      | CompSt ENTER
      | ReturnStmt ENTER
      | IfElseStmt ENTER
      | RepeatStmt ENTER
ReturnStmt -> RETURN ReturnParam | RETURN
ReturnParam -> NoAssignExp | ListAndTuple | Dictionary

IfStmt -> IF NoAssignExp CompSt
ElifStmt -> IfStmt |
            ElifStmt elif NoAssignExp CompSt
IfElseStmt -> ElifStmt
            | IfElseStmt ELSE CompSt

RepeatStmt -> REPEAT RepeatCond CompSt
RepeatParam -> IDENTIFIER | RepeatParam , IDENTIFIER
IterateValue -> Variable
              | Tuple
              | List
RepeatCond -> NoAssignExp
            | RepeatParam IN IterateValue



FuncInvocation -> IDENTIFIER ( ) | IDENTIFIER ( Args )

AssignableValue -> Variable . AssignableValue
                 | Variable [ Variable ]
                 | Variable [ STRING ]
                 | Variable [ NUM ]
                 | Variable [ NoAssignExp ]
                 | IDENTIFIER

Variable -> AssignableValue
          | Variable . FuncInvocation
          | FuncInvocation
          | SELF

Primary -> ( NoAssignExp )
         | Variable
         | NUM
         | REAL
         | TRUE
         | FALSE
         | STRING

UnaryExp -> Primary
          | - Primary
          | NOT Primary

MultiplicativeExp -> UnaryExp
                   | MultiplicativeExp * UnaryExp
                   | MultiplicativeExp / UnaryExp
                   | MultiplicativeExp % UnaryExp
AdditiveExp -> MultiplicativeExp
             | AdditiveExp + MultiplicativeExp
             | AdditiveExp - MultiplicativeExp
RelationExp -> AdditiveExp
             | RelationExp < AdditiveExp
             | RelationExp > AdditiveExp
             | RelationExp <= AdditiveExp
             | RelationExp >= AdditiveExp
EqualityExp -> RelationExp
             | EqualityExp == RelationExp
             | EqualityExp != RelationExp
ConditionAndExp -> EqualityExp
             | ConditionAndExp AND EqualityExp
ConditionOrExp -> ConditionAndExp
                | ConditionOrExp OR ConditionAndExp
NoAssignExp -> ConditionOrExp
AssignmentExp -> NoAssignExp
               | Assignment
Assignment -> LeftSide = AssignmentExp
            | MultiAssignment
LeftSide -> AssignableValue | ListAndTuple
MultiAssignment -> ListAndTuple = ListAndTuple
                | AssignableValue = Dictionary
                | AssignableValue = ListAndTuple

Exp -> AssignmentExp
Args -> Args , Exp | Exp


Stmt -> BREAK
      | CONTINUE


